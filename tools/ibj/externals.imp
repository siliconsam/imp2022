
!----------------------------------------------------------------------
! external symbols
!----------------------------------------------------------------------
%constinteger max externals = 50
%record %format external( %integer localid,
                          %string(127) localname,
                          %integer id,
                          %string(127) name)
%own %record(external)%array externals(1:max externals)
%own %integer external count

!----------------------------------------------------------------------
! new external: DEFEXTCODE, DEFEXTDATA
!----------------------------------------------------------------------
%external %integer %fn new external( %string(127) external name )
    %if (external count < max externals) %start
        external count = external count + 1
        externals( external count )_id = external count
        externals( external count )_name = external name
    %finish %else %start
        debug string( "Max externals =" )
        debug string( itos(max externals,0) )
        debug newline
        debug string( "Too many external names already whilst trying to add " )
        debug string( external name )
        debug newline
    %finish

    %result = external count
%end

!----------------------------------------------------------------------
! get external name: DEFEXTCODE, DEFEXTDATA
!----------------------------------------------------------------------
%external %string(127)%fn get external name( %integer externalid )
    %string(127) name

    name = externals( externalid )_name

    %result = name
%end

!----------------------------------------------------------------------
! get external id: DEFEXTCODE, DEFEXTDATA
!----------------------------------------------------------------------
%external %integer %fn get external id( %integer externalid )
    %integer id

    id = externals( externalid )_id

    %result = id
%end

!----------------------------------------------------------------------
! associate local: DEFEXTCODE, FIXUP, DEFEXTDATA, LABEL
!----------------------------------------------------------------------
%external %routine associate local( %integer externalid,
                                    %integer localid,
                                    %string(127) name )
    externals( externalid )_localid   = localid
    externals( externalid )_localname = name
%end

!----------------------------------------------------------------------
! get associate name: DEFEXTCODE, FIXUP, DEFEXTDATA, LABEL
!----------------------------------------------------------------------
%external %string(127)%fn get associate name( %integer externalid )
    %string(127) name

    name = externals( externalid )_localname

    %result = name
%end

!----------------------------------------------------------------------
! get associate id: DEFEXTCODE, FIXUP, DEFEXTDATA, LABEL
!----------------------------------------------------------------------
%external %integer %fn get associate id( %integer externalid )
    %integer localid

    localid = externals( externalid )_localid

    %result = localid
%end

!----------------------------------------------------------------------
! dump externals
!----------------------------------------------------------------------
%external %routine dump externals
    %integer i

    newline
    print string( "Dumping externals" ); newline

    %for i = 1,1,external count %cycle
        print symbol( '[' )
        print string( itos(i,5) )
        print symbol( '/' )
        print string( itos(externals(i)_id,5) )
        print symbol( ']' )
        print symbol( ' ' )
        print symbol( '"' )
        print string( externals(i)_name )
        print symbol( '"' )
        %if (externals(i)_localid # 0) %start
            print symbol( ',' )
            print string( itos(externals(i)_id,5) )
            print symbol( ',' )
            print symbol( '"' )
            print string( externals(i)_localname )
            print symbol( '"' )
        %finish
        newline
    %repeat
%end

!----------------------------------------------------------------------
! initialise externals
!----------------------------------------------------------------------
%external %routine initialise externals
    %integer i

    %for i = 1,1,max externals %cycle
        externals(i)_localid = 0
        externals(i)_localname = ""
        externals(i)_id = 0
        externals(i)_name = ""
    %repeat
    external count = 0
%end

%endoffile
